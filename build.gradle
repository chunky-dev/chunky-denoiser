plugins {
    id 'org.openjfx.javafxplugin' version '0.0.10'
    id 'maven-publish'
}
jar.enabled = false

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

configurations {
    implementation.extendsFrom(provided)
    implementation.extendsFrom(bundled)
}

dependencies {
    provided 'se.llbit:chunky-core:2.4.0'
    provided 'org.apache.commons:commons-math3:3.2'
    provided 'it.unimi.dsi:fastutil:8.4.4'
    provided 'se.llbit:jo-json:1.3.1'
}

task pluginJar(type: Jar) {
    with jar
}

jar {
    manifest {
        attributes "Main-Class": "de.lemaik.chunky.denoiser.DenoiserPlugin"
    }

    from {
        configurations.bundled.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

javafx {
    version = '17'
    modules = ['javafx.base', 'javafx.controls', 'javafx.fxml']
    configuration = 'provided'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'https://repo.lemaik.de/'
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'de.lemaik'
            artifactId = 'chunky-denoiser'
            version = '0.5.0'
            from components.java

            pom {
                name = 'chunky-denoiser'

                licenses {
                    license {
                        name = 'GNU General Public License v3.0 (GPL-3.0)'
                        url = 'https://github.com/chunky-dev/chunky-denoiser/blob/master/LICENSE'
                    }
                }
            }
        }
    }
}
